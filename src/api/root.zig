const std = @import("std");

pub const abi = @import("abi");
pub const protocol = @import("protocol.zig");
pub const Terminal = @import("terminal.zig").Terminal;
pub const files = @import("files.zig");
pub const Files = files.Files;
pub const Accounts = @import("accounts.zig").Accounts;
pub const display = @import("display.zig");
pub const Display = display.Display;
pub const Event = @import("event.zig").Event;
pub const Key = @import("event.zig").Key;
pub const Request = abi.Request;
pub const ApiError = error{ Denied, Invalid, Missing, Deadlock, Exhausted, Timeout, Busy };
pub const ServiceError = ApiError || error{ Exists, Full };
var environment: ?*const abi.Environment = null;

extern fn invoke(request: *Request) callconv(.c) void;

pub const panic = std.debug.FullPanic(fatal);

pub fn start(context: *const abi.Environment, expected: abi.Layer) void {

    if (context.version != 1 or context.length > abi.permission_count or context.layer != expected) exit(97);
    environment = context;

}

pub fn permissions() []const abi.Permission {

    const context = environment orelse exit(97);

    return context.permissions[0..context.length];

}

pub fn permits(permission: abi.Permission) bool {

    for (permissions()) |entry| {

        if (entry == permission) return true;

    }

    return false;

}

pub fn raw(number: abi.Call, first: u64, second: u64, third: u64) Request {

    var request = Request{

        .number = @intFromEnum(number),
        .first = first,
        .second = second,
        .third = third,

    };

    invoke(&request);

    return request;

}

pub fn checked(request: Request) ApiError!Request {

    return switch (request.number) {

        0 => request,
        1 => error.Denied,
        3 => error.Missing,
        4 => error.Deadlock,
        5 => error.Exhausted,
        6 => error.Timeout,
        7 => error.Busy,
        else => error.Invalid,

    };

}

/// A timeout or disconnect does not guarantee the peer performed no work.
pub fn call(peer: u64, message: u64) ApiError!u64 {

    return (try checked(raw(.call, peer, message, 100))).first;

}

/// Lends `window` to the peer until it replies; the peer reads and writes it with `fetch` and `store`.
pub fn exchange(peer: u64, message: u64, window: []u8) ApiError!u64 {

    var request = Request{

        .number = @intFromEnum(abi.Call.call),
        .first = peer,
        .second = message,
        .third = 1000,
        .fourth = @intFromPtr(window.ptr),
        .fifth = window.len,

    };

    invoke(&request);

    return (try checked(request)).first;

}

/// Copies from the window lent by a pending `request` into `bytes`.
pub fn fetch(request: Request, offset: u64, bytes: []u8) ApiError!void {

    try transfer(.fetch, request, offset, @intFromPtr(bytes.ptr), bytes.len);

}

/// Copies `bytes` into the window lent by a pending `request`.
pub fn store(request: Request, offset: u64, bytes: []const u8) ApiError!void {

    try transfer(.store, request, offset, @intFromPtr(bytes.ptr), bytes.len);

}

fn transfer(number: abi.Call, request: Request, offset: u64, address: u64, length: u64) ApiError!void {

    var copy = Request{

        .number = @intFromEnum(number),
        .first = request.first,
        .second = request.third,
        .third = offset,
        .fourth = address,
        .fifth = length,

    };

    invoke(&copy);
    _ = try checked(copy);

}

pub const Page = struct {

    bytes: *align(4096) [4096]u8,
    physical: u64,

};

/// Allocates a zeroed page that devices may access directly.
pub fn dma() ApiError!Page {

    const result = try checked(raw(.dma, 0, 0, 0));

    return .{

        .bytes = @ptrFromInt(result.first),
        .physical = result.second,

    };

}

/// Memory that other processes may attach once lent to them; its pages need not be physically adjacent.
pub const Shared = struct {

    handle: u64,
    bytes: []align(4096) u8,

};

pub fn share(pages: usize) ApiError!Shared {

    return mapped(try checked(raw(.share, pages, 0, 0)));

}

/// Lets process `peer` attach `handle`.
pub fn lend(handle: u64, peer: u64) ApiError!void {

    _ = try checked(raw(.lend, handle, peer, 0));

}

pub fn attach(handle: u64) ApiError!Shared {

    return mapped(try checked(raw(.attach, handle, 0, 0)));

}

pub fn detach(handle: u64) void {

    _ = raw(.detach, handle, 0, 0);

}

fn mapped(result: Request) Shared {

    return .{

        .handle = result.second,
        .bytes = @as([*]align(4096) u8, @ptrFromInt(result.first))[0 .. result.third * 4096],

    };

}

/// The physical address behind `address`, for drivers programming devices.
pub fn physical(address: usize) ApiError!u64 {

    return (try checked(raw(.physical, address, 0, 0))).first;

}

/// Whether process `id` still exists.
pub fn alive(id: u64) bool {

    return raw(.alive, id, 0, 0).number == 0;

}

/// Maps `size` bytes of granted device memory at `base` as one consecutive range.
pub fn map(base: u64, size: u64) ApiError![*]u8 {

    const page = base & ~@as(u64, 4095);
    const first = (try checked(raw(.map, page, 0, 0))).first;
    var offset: u64 = 4096;

    while (offset < base + size - page) : (offset += 4096) {

        if ((try checked(raw(.map, page + offset, 0, 0))).first != first + offset) return error.Exhausted;

    }

    return @ptrFromInt(first + base % 4096);

}

pub fn in32(port: u64) ApiError!u32 {

    return @intCast((try checked(raw(.port, port, 2, 0))).first);

}

pub fn out32(port: u64, value: u32) ApiError!void {

    _ = try checked(raw(.port, port, 3, value));

}

pub fn receive(block: bool) ApiError!Request {

    return try checked(raw(.receive, if (block) 0 else 1, 0, 0));

}

pub fn reply(request: Request, message: u64) ApiError!void {

    _ = try checked(raw(.reply, request.first, request.third, message));

}

/// Exchanges `window` with a supervised service, looking it up afresh once if a restart replaced it.
pub fn query(endpoint: *u64, image: abi.Image, message: u64, window: []u8) ServiceError!u64 {

    for (0..2) |_| {

        if (endpoint.* == 0) endpoint.* = try lookup(0, image);

        const result = exchange(endpoint.*, message, window) catch |err| {

            endpoint.* = 0;
            if (err == error.Missing or err == error.Denied) continue;

            return err;

        };

        return switch (result) {

            protocol.missing => error.Missing,
            protocol.exists => error.Exists,
            protocol.full => error.Full,
            protocol.busy => error.Busy,
            protocol.denied => error.Denied,
            protocol.invalid => error.Invalid,
            else => result,

        };

    }

    return error.Missing;

}

/// Starts app `name` running as the caller, in the caller's session; only signed-in sessions may.
pub fn launch(name: []const u8) ServiceError!void {

    var bytes: [32]u8 = undefined;
    if (name.len > bytes.len) return error.Invalid;

    @memcpy(bytes[0..name.len], name);
    try supervised(try exchange(0, protocol.pack(.launch, 0), bytes[0..name.len]));

}

/// Stops every app of the caller's session; the login process calls it on logout.
pub fn end() ServiceError!void {

    try supervised(try call(0, protocol.pack(.end, 0)));

}

/// Supervisor-only: spawns `image` as an application running as the sender of `request`, which must still await our reply.
pub fn spawn(image: abi.Image, request: Request) ApiError!u64 {

    var message = Request{

        .number = @intFromEnum(abi.Call.spawn),
        .first = @intFromEnum(image),
        .third = request.first,
        .fourth = request.third,

    };

    invoke(&message);

    return (try checked(message)).first;

}

fn supervised(result: u64) ServiceError!void {

    return switch (result) {

        protocol.missing => error.Missing,
        protocol.full => error.Full,
        protocol.denied => error.Denied,
        protocol.invalid => error.Invalid,
        else => {

        },

    };

}

/// The identity the kernel attached to a received message.
pub fn sender(message: Request) abi.Identity {

    return @bitCast(message.fifth);

}

/// Reads (`write` false) or writes a global firmware variable; returns the variable's size.
pub fn variable(name: []const u16, data: []u8, write: bool) ApiError!usize {

    var message = Request{

        .number = @intFromEnum(abi.Call.variable),
        .first = @intFromPtr(name.ptr),
        .second = name.len,
        .third = @intFromPtr(data.ptr),
        .fourth = data.len,
        .fifth = @intFromBool(write),

    };

    invoke(&message);

    return (try checked(message)).first;

}

/// Fills `bytes` from the CPU's hardware random generator.
pub fn random(bytes: []u8) void {

    var index: usize = 0;

    while (index < bytes.len) : (index += 8) {

        const value = std.mem.toBytes(entropy());
        const size = @min(8, bytes.len - index);

        @memcpy(bytes[index..][0..size], value[0..size]);

    }

}

fn entropy() u64 {

    for (0..10) |_| {

        var ready: u8 = undefined;
        const value = asm volatile ("rdrand %[value]; setc %[ready]" // Draw from the hardware generator.
            : [value] "=r" (-> u64),
              [ready] "=r" (ready),
        );

        if (ready != 0) return value;

    }

    @panic("Hardware random generator failed");

}

pub fn lookup(supervisor: u64, image: abi.Image) ApiError!u64 {

    const id = try call(supervisor, protocol.pack(.lookup, @intCast(@intFromEnum(image))));
    if (id == 0 or id == protocol.invalid) return error.Missing;

    return id;

}

pub fn sleep(duration: u64) void {

    _ = raw(.sleep, duration, 0, 0);

}

pub fn ticks() u64 {

    return raw(.ticks, 0, 0, 0).first;

}

pub fn identity() u64 {

    return raw(.identity, 0, 0, 0).first;

}

pub fn log(bytes: []const u8) void {

    _ = raw(.write, @intFromPtr(bytes.ptr), bytes.len, 0);

}

pub fn exit(status: u64) noreturn {

    _ = raw(.exit, status, 0, 0);

    while (true) asm volatile ("ud2");

}

fn fatal(_: []const u8, _: ?usize) noreturn {

    exit(98);

}
